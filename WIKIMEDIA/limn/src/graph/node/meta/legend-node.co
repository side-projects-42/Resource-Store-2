_  = require 'underscore'
ko = require 'knockout'
moment = require 'moment'

{ guidFor, formatters, OrderedMap,
} = require '../../../util'
{ AttributesBase,
} = require '../../../base'
{ GraphNodeData,
} = require '../graph-node-data'
{ GraphNode,
} = require '../graph-node'

Trait = require '../graph-node-trait'



/**
 * @class
 * @extends GraphNodeData
 */
class exports.LegendNodeData extends GraphNodeData
    @registerType 'legend'
    
    defaults: ->
        label   : null
        options : 
            dateFormat  : 'MMM YYYY'
            valueFormat : ',.2s'
    
    attributeTypes: ->
        {}
    
    -> super ...



/**
 * @class
 * @extends GraphNode
 */
class exports.LegendNode extends GraphNode
    @registerType 'legend'
    traits : [
        Trait.SECTION
        Trait.META_NODE
        Trait.REQUIRES_METRIC_POINTER
        Trait.METRIC_TIMESERIES
        Trait.HTML
        Trait.LEAF
    ]
    tagName  : 'section'
    template : 'legend-node'
    
    
    /**
     * The `LegendEntry` cache.
     * @protected
     * @type OrderedMap<GraphNode, LegendEntry>
     */
    entryCache : null
    
    
    ->
        @entryCache = new OrderedMap()
        super ...
        @date = ko.observable()
    
    
    buildElement: (parentElement) ->
        el = super ...
        $ parentElement .prepend el
        @renderView el
    
    
    nodes: @computed ->
        @viewport().filterTraits [Trait.VIS_NODE, Trait.REQUIRES_METRIC]
    
    
    entries: @computedRequires 'nodes', (nodes) ->
        nodes
            .filter (node) ->
                not node.options()?.get('noLegend')
            .map (node) ~>
                @lookupEntry node
    
    
    lookupEntry: (node) ->
        return null unless node?
        return that if @entryCache.get node
        
        entry = new LegendEntry this, node
        @entryCache.set node, entry
        
        # register for notification when the node is destroyed
        destroySub = node.isDisposed.subscribe (isDisposed) ~>
            throw new Error "WTF! node.isDisposed false for #node!" unless isDisposed
            currentEntry = @entryCache.get node
            @entryCache.remove node if currentEntry is entry
            destroySub.dispose()
        
        entry
    
    
    dateFormatter : @computedRequires 'options', (options) ->
        @dateFormatterFor options.get('dateFormat')
    
    
    watchOthers: ->
        @trackHover()
    
    
    /**
     * Track hover events and change the legend date.  This can be used by legend entries.
     */
    trackHover: @computed ->
        return unless viewport = @viewport()
        return unless invert = viewport.xScale()?.invert
        return unless cursor = @root().cursor()
        @date invert cursor.x if cursor.hovering
    
    


/**
 * @class
 * @extends AttributesBase
 */
class exports.LegendEntry extends AttributesBase implements formatters
    
    defaults: ->
        value : null
        date  : null
    
    
    (@legend, @node) ->
        super {}
        @legendOptions = @legend.options
        @{ options, label, disabled } = node
        if @legendOptions().shortLabels?()
            @label = node.shortLabel
        @legend.date.subscribe @trackLegendDate, this
    
    
    /**
     * handle toggling of legend entries, bound with knockout
     */
    toggleVisibility: ->
        # if this is the last visible entry, don't disable it
        # TODO: find out why on Dario's dashboards disabling the last metric created an invalid state
        return unless @node.disabled()
            or _.countBy(@legend.entries(), -> it.node.disabled()).false > 1
        @node.options().set 'disabled', !@node.disabled()
    
    
    trackLegendDate: (newDate) ->
        return unless @node?.metric()?.data()?.length
        
        if newDate
            closest = @node.metric().findClosest newDate
            @date closest.date
            @value @valueFormatter() closest.value
            
            @legend.label @dateFormatter() @date()
    
    
    nodeId: @computed ->
        guidFor @node
    
    color: @computed ->
        if @disabled() then '#ddd' else @node.strokeColor()
    
    dateFormatter : @computedRequires 'legendOptions', 'options', (legendOptions, options) ->
        dateFormat = legendOptions.get('dateFormat') or options.get('dateFormat')
        @dateFormatterFor dateFormat
    
    valueFormatter : @computedRequires 'legendOptions', 'options', (legendOptions, options) ->
        # valueFormat = legendOptions.get('valueFormat') or options.get('valueFormat')
        @numberFormatterHTMLFor digits=2, abbrev=true, addWrapper=false
    
    dateReady : @computedRequires 'node.timeseriesData', 'dateFormatter', (data, dateFormatter) ->
        [date, val] = _.last(data)
        @date dateFormatter date
    
    valueReady : @computedRequires 'node.timeseriesData', 'valueFormatter', (data, valueFormatter) ->
        [date, val] = _.last(data)
        @value valueFormatter val
