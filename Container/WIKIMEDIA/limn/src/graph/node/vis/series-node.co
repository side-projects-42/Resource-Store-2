_  = require 'underscore'
ko = require 'knockout'

{ GraphNodeData,
} = require '../graph-node-data'
{ GraphNode,
} = require '../graph-node'
{ Metric,
} = require '../../../data/metric'

Trait = require '../graph-node-trait'
ProjectColors = require '../../project-colors'


/**
 * @class
 * @extends GraphNodeData
 */
class exports.SeriesNodeData extends GraphNodeData
    @registerType 'series'
    
    defaults: ->
        metric : null
    
    attributeTypes: ->
        metric : Metric
    
    -> super ...



/**
 * @class
 * @extends GraphNode
 */
class exports.SeriesNode extends GraphNode
    @registerType 'series'
    traits : [
        Trait.VIS_NODE
        Trait.REQUIRES_METRIC
        Trait.METRIC_CONTENT_DATA
        Trait.SVG
        Trait.LEAF
    ]
    
    -> super ...
    
    
    /**
     * Materialized series data.
     * @type ko.computed<Array<[Index, Value]>>
     */
    seriesData: @computedRequires 'model.metric', (metric) ->
        metric.data()
    
    
    /**
     * Label determined from the following, in order
     *   options.label
     *   metric.defaultLabel
     *   '(no label)'
     * @type ko.computed<String>
     */
    shortLabel: @computedRequires 'model.options', (options) ->
        options.label?() or @model().metric()?.defaultLabel() or '(no label)'
    
    
    /**
     * Label determined from the following, in order
     *   options.label
     *   metric.defaultLongLabel
     *   '(no label)'
     * @type ko.computed<String>
     */
    label: @computedRequires 'model.options', (options) ->
        options.label?() or @model().metric()?.defaultLongLabel() or '(no label)'
    
    
    /**
     * Palette using defaults
     */
    palette: @computedRequires 'model.options', (options) ->
        options.get 'palette'
    
    
    /**
     * @type ko.computed<String := hex color | rgb() color>
     */
    paletteColor: @computedRequires 'model.options', (options) ->
        ProjectColors.lookup(@shortLabel(), options.get('palette'))
    
    
